// DIFFUSION STEP:
// Iteratively diffuses the liquid, causing viscous fluids to slow
// Solves for Ax = b, where A is the computations performed, x is
// the pressure input, and b is the pressure as output. Needs to run
// many iterations in order to achieve convincing result. (20 to 50)

#version 430 core
layout (local_size_x = 512) in; // workgroup size

layout (rgba8, binding = 0) uniform image2D xVecField; //x
layout (rgba8, binding = 1) uniform image2D bVecField; //b
layout (rgba8, binding = 2) uniform image2D result;

//alpha = (dx)^2/Vdt, where dx^2 = factor of grid size
layout (location = 0) uniform float alpha;

//reciprical of beta (1/beta), (beta = 4 + alpha)
layout (location = 1) uniform float rBeta;

void main()
{
	ivec2 curCoord = ivec2(gl_GlobalInvocationID.xy);

	//sample around target location using offset
	vec4 xleft  = imageLoad(xVecField, ivec2(mod(curCoord.x+512 - 1, 512), curCoord.y               ));
	vec4 xright = imageLoad(xVecField, ivec2(mod(curCoord.x     + 1, 512), curCoord.y               ));
	vec4 xbelow = imageLoad(xVecField, ivec2(curCoord.x                  , max(curCoord.y - 1, 0  ) ));
	vec4 xabove = imageLoad(xVecField, ivec2(curCoord.x                  , min(curCoord.y + 1, 511) ));

	//sample from center of b
	vec4 bcenter = imageLoad(bVecField, curCoord);

	//evaluate jacobi iteration
	vec4 texel = (xleft + xright + xbelow + xabove + alpha * bcenter) * rBeta;

	//store result
	imageStore(result, curCoord, texel);
}
